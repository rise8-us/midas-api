plugins {
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'org.flywaydb.flyway' version '8.0.0'
    id 'antlr'
    id 'checkstyle'
    id 'jacoco'
}

group = 'mil.af.abms'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

flyway {
    driver = 'com.mysql.cj.jdbc.Driver'
    url = 'jdbc:mysql://${System.env.MYSQL_HOST}:${System.env.MYSQL_PORT}/${System.env.MYSQL_DB_NAME}?useSSL=false'
    user = '${System.env.MYSQL_DB_ADMIN_USER}'
    password = '${System.env.MYSQL_DB_ADMIN_PASSWORD}'
    table = '${System.env.MYSQL_DB_NAME}'
}

sourceSets {
    main {
        java {
            srcDir '${buildDir.absolutePath}/generated-src/antlr/main'
        }
    }
}

dependencies {
    antlr 'org.antlr:antlr4:4.9.3'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.security:spring-security-messaging'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.hibernate:hibernate-validator:7.0.1.Final'
    implementation 'org.flywaydb:flyway-core:8.0.2'
    implementation 'com.auth0:java-jwt:3.18.2'
    implementation 'org.gitlab4j:gitlab4j-api:4.18.0'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.111'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    runtimeOnly 'mysql:mysql-connector-java:8.0.26'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.h2database:h2:1.4.200'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute(module("com.ibm.icu:icu4j")).using(module("com.ibm.icu:icu4j:69.1"))
        substitute(module("org.apache.logging.log4j:log4j-api")).using(module("org.apache.logging.log4j:log4j-api:2.17.0"))
        substitute(module("org.apache.logging.log4j:log4j-to-slf4j")).using(module("org.apache.logging.log4j:log4j-to-slf4j:2.17.0"))
    }
}

generateGrammarSource {
    arguments += ['-visitor', '-no-listener','-package', 'mil.af.abms.midas.api.search']

    doLast {
        copy {
            from 'build/generated-src/antlr/main/'
            include '*.*'
            into 'build/generated-src/antlr/main/mil/af/abms/midas/api/search'
        }
        project.delete fileTree('build/generated-src/antlr/main').include('*.*')
    }
}
compileJava.dependsOn generateGrammarSource

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
        exceptionFormat 'full'
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'mil/af/abms/midas/api/search/RestQueryBaseVisitor**',  //Generated by ANTLR
                    'mil/af/abms/midas/api/search/RestQueryParser**',       //      |
                    'mil/af/abms/midas/api/search/RestQueryLexer**',        //Generated by ANTLR
                    'mil/af/abms/midas/clients/MySQLClient*',
                    'mil/af/abms/midas/config/Startup*',
                    'mil/af/abms/midas/config/AppConfig*',
                    '**/dto/**DTO**',
                    'mil/af/abms/midas/exception/**'
            ])
        }))
    }
    reports {
        xml.required = true
        html.required = true
    }
    dependsOn test
}

checkstyleMain.source = 'src/main/java'

tasks.register('lintGradle') {
    dependsOn checkstyleMain, checkstyleTest
}
